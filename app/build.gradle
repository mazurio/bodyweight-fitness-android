apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

def getKey(key) {
    def Properties properties = new Properties()

    properties.load(new FileInputStream(new File('api.properties')))

    return properties[key]
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    defaultConfig {
        applicationId 'io.mazur.fit'
        minSdkVersion 14
        targetSdkVersion 23
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
        pro {
            applicationId 'com.bodyweight.fitness.pro'
            versionCode 114
            versionName "1.1.4"
        }

        free {
            applicationId 'com.bodyweight.fitness.free'
            versionCode 114
            versionName "1.1.4"
        }
    }
    buildTypes {
        applicationVariants.all { variant ->
            variant.buildConfigField "String", "AMAZON_API_KEY", "\"" + getKey("AMAZON_API_KEY") + "\""
            variant.buildConfigField "String", "IDENTITY_POOL_ID", "\"" + getKey("IDENTITY_POOL_ID") + "\""
        }

        debug {
            ext.enableCrashlytics = false
        }
    }
}

configurations {
    all*.exclude group: 'io.reactivex', module: 'rxjava'
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.github.mazurio:glacier:0.0.3'
    compile 'net.danlew:android.joda:2.7.1'
    compile 'commons-io:commons-io:2.4'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.1'
    compile 'com.github.johnkil.android-robototextview:robototextview:2.3.0'
    compile 'com.gordonwong:material-sheet-fab:1.2'
    compile 'com.github.bmarrdev:android-DecoView-charting:v0.9.4'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'frankiesardo:icepick:3.1.0'
    provided 'frankiesardo:icepick-processor:3.1.0'

    compile 'com.amazonaws:aws-android-sdk-core:2.2.10'
    compile 'com.amazonaws:aws-android-sdk-lambda:2.2.10'
    compile 'com.amazonaws:aws-android-sdk-cognito:2.2.10'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.6@aar') {
        transitive = true;
    }
    compile files('libs/amazon-mobile-associates-1.0.73.0.jar')
}
